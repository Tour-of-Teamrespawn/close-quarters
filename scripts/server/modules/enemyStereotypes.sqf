// Items

private _casualUniforms = [
	"UK3CB_CHC_C_U_COACH_03",
	2,
	"UK3CB_CHC_C_U_COACH_04",
	2,
	"UK3CB_CHC_C_U_COACH_01",
	2,
	"UK3CB_ADC_C_Pants_U_04",
	1,
	"UK3CB_ADC_C_Pants_U_17",
	1,
	"UK3CB_ADC_C_Pants_U_05",
	1,
	"UK3CB_ADC_C_Pants_U_01",
	1,
	"UK3CB_ADC_C_Pants_U_02",
	1,
	"UK3CB_ADC_C_Pants_U_10",
	1,
	"UK3CB_ADC_C_Pants_U_09",
	1,
	"U_C_Poloshirt_burgundy",
	1,
	"UK3CB_ADC_C_Shorts_U_03",
	1,
	"UK3CB_CHC_C_U_Overall_05",
	1,
	"UK3CB_CHC_C_U_ACTIVIST_01",
	1,
	"U_C_ArtTShirt_01_v5_F",
	1
];

Foley_fatUniforms = [
	"UK3CB_CHC_C_U_COACH_03",
	"UK3CB_CHC_C_U_COACH_04",
	"UK3CB_CHC_C_U_COACH_01",
	"UK3CB_CHC_C_U_ACTIVIST_01",
	"UK3CB_CHC_C_U_Overall_05"
];

private _combatUniforms = [
	"UK3CB_ADE_O_U_02_K",
	2,
	"UK3CB_ADE_O_U_02_E",
	2,
	"UK3CB_ADE_O_U_02_J",
	2,
	"UK3CB_ADM_B_U_Tshirt_01_DPM",
	2,
	"UK3CB_ADM_B_U_Tshirt_01_WDL_02",
	2,
	"UK3CB_ADM_B_U_Tshirt_01_TCC",
	2,
	"UK3CB_NAP_B_U_Tshirt_FLK",
	2,
	"UK3CB_NAP_B_U_Tshirt_BLK",
	2,
	"U_BG_Guerilla2_2",
	1,
	"U_BG_Guerrilla_6_1",
	1,
	"UK3CB_BAF_U_JumperUniform_MTP",
	1,
	"U_BG_Guerilla1_1",
	1,
	"UK3CB_CW_US_B_Early_U_SF_CombatUniform_03_ERDL_OLI",
	1,
	"UK3CB_ADM_B_U_Shirt_Pants_01_BRN_CC",
	1,
	"U_I_C_Soldier_Para_4_F",
	0.5,
	"U_I_C_Soldier_Para_1_F",
	0.5,
	"U_I_G_Story_Protagonist_F",
	0.5
];

private _casualHeadgear = [
	"",
	6,
	"UK3CB_CHC_C_H_Can_Cap",
	4,
	"UK3CB_H_Profiteer_Cap_01",
	2,
	"H_MilCap_gry",
	2,
	"UK3CB_ADA_B_H_BoonieHat_TTC",
	1,
	"H_Cap_blk",
	1,
	"H_Cap_blu",
	1,
	"H_Cap_blk_ION",
	1,
	"H_Cap_tan",
	1,
	"H_Cap_blk_Raven",
	1,
	"rhs_fieldcap_ml",
	1,
	"H_HeadBandage_stained_F",
	1
];

Foley_casualHeadgear = _casualHeadgear;

private _combatHeadgear = [
	"UK3CB_TKP_I_H_SSh68_BLK",
	5,
	"rhsgref_helmet_pasgt_olive",
	1,
	"rhsgref_6b27m_ttsko_urban",
	1,
	"rhs_6b27m_green",
	1
];

private _noVest = [
	"",
	1
];

private _lightVests = [
	"UK3CB_ADA_B_V_TacVest_BLK",
	1,
	"V_TacVest_camo",
	1,
	"UK3CB_ADA_B_V_TacVest_CC",
	1,
	"V_TacVest_khk",
	1
];

private _heavyVests = [
	"UK3CB_AAF_I_V_Falcon_1_DIGI_BRN",
	4,
	"UK3CB_AAF_O_V_Eagle_CREW_DIGI_BLK",
	4,
	"rhs_6b5_khaki",
	3,
	"UK3CB_BAF_V_Osprey",
	1,
	"UK3CB_BAF_V_Osprey_Lite",
	1
];

private _casualFacewear = [
	"",
	10,
	"G_Aviator",
	2,
	"G_Shades_Black",
	2,
	"G_Squares_Tinted",
	2,
	"G_Squares",
	2,
	"G_Shades_Green",
	1,
	"G_Sport_Red",
	1,
	"G_Sport_BlackWhite",
	1,
	"G_Sport_Blackred",
	1,
	"G_Sport_Greenblack",
	1,
	"G_WirelessEarpiece_F",
	1
];

private _susFacewear = [
	"G_Balaclava_blk",
	10,
	"UK3CB_G_Face_Wrap_01",
	3,
	"UK3CB_BAF_G_Tactical_Yellow",
	2,
	"UK3CB_BAF_G_Tactical_Clear",
	2,
	"G_Bandanna_aviator",
	1,
	"G_Bandanna_khk",
	1,
	"G_Bandanna_shades",
	1,
	"rhsusf_shemagh2_tan",
	1,
	"rhsusf_shemagh2_gogg_tan",
	0.5,
	"rhsusf_shemagh2_gogg_white",
	0.5,
	"G_Bandanna_beast",
	0.5
];

private _weaponsTier1 = [
	[
		"rhsusf_weap_m9",
		"rhsusf_mag_15Rnd_9x19_FMJ"
	],
	5,
	[
		"rhs_weap_makarov_pm",
		"rhs_mag_9x18_8_57N181S"
	],
	1,
	[
		"hgun_Pistol_heavy_02_F",
		"6Rnd_45ACP_Cylinder"
	],
	1
];

private _weaponsTier2 = [
	[
		"UK3CB_MP5",
		"30Rnd_9x21_Mag_SMG_02_Tracer_Red"
	],
	5,
	[
		"hgun_PDW2000_F",
		"30Rnd_9x21_Mag"
	],
	1
];

private _weaponsTier3 = [
	[
		"rhs_weap_aks74_2",
		"rhs_30Rnd_545x39_7N6M_AK"
	],
	3,
	[
		"rhs_weap_akm",
		"rhs_30Rnd_762x39mm"
	],
	2,
	[
		"srifle_DMR_06_hunter_F",
		"10Rnd_Mk14_762x51_Mag"
	],
	1,
	[
		"rhs_weap_M590_8RD",
		"rhsusf_5Rnd_Slug"
	],
	1
];

// Stereotypes

private _unprotectedTier1 = [
	[_casualUniforms, 80, _combatUniforms, 20],
	[_noVest, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 70, _susFacewear, 30],
	[_weaponsTier1, 100]
];

private _unprotectedTier2 = [
	[_casualUniforms, 70, _combatUniforms, 30],
	[_noVest, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 60, _susFacewear, 40],
	[_weaponsTier2, 100]
];

private _unprotectedTier3 = [
	[_casualUniforms, 60, _combatUniforms, 40],
	[_noVest, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 50, _susFacewear, 50],
	[_weaponsTier3, 100]
];

private _lightTier1 = [
	[_casualUniforms, 80, _combatUniforms, 20],
	[_lightVests, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 70, _susFacewear, 30],
	[_weaponsTier1, 100]
];

private _lightTier2 = [
	[_casualUniforms, 30, _combatUniforms, 70],
	[_lightVests, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 50, _susFacewear, 50],
	[_weaponsTier2, 100]
];

private _lightTier3 = [
	[_casualUniforms, 10, _combatUniforms, 90],
	[_lightVests, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 40, _susFacewear, 60],
	[_weaponsTier2, 100]
];

private _mediumTier2 = [
	[_combatUniforms, 100],
	[_heavyVests, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 30, _susFacewear, 70],
	[_weaponsTier2, 100]
];

private _mediumTier3 = [
	[_combatUniforms, 100],
	[_heavyVests, 100],
	[_casualHeadgear, 100],
	[_casualFacewear, 30, _susFacewear, 70],
	[_weaponsTier3, 100]
];

private _heavyTier2 = [
	[_combatUniforms, 100],
	[_heavyVests, 100],
	[_combatHeadgear, 100],
	[_casualFacewear, 10, _susFacewear, 90],
	[_weaponsTier2, 100]
];

private _heavyTier3 = [
	[_combatUniforms, 100],
	[_heavyVests, 100],
	[_combatHeadgear, 100],
	[_susFacewear, 100],
	[_weaponsTier3, 100]
];

Foley_stereotypesByEquipmentLevel = [
	[
		1,
		[_unprotectedTier1, 100]
	],
	[
		2,
		[_unprotectedTier2, 70, _lightTier1, 30]
	],
	[
		3,
		[_unprotectedTier3, 40, _lightTier2, 60]
	],
	[
		4,
		[_lightTier3, 50, _mediumTier2, 50]
	],
	[
		5,
		[_mediumTier3, 70, _heavyTier2, 30]
	],
	[
		6,
		[_heavyTier3, 100]
	]
];

Foley_fnc_generateLoadout = {
	params ["_equipmentLevel"];

	private _applicableStereotypes = Foley_stereotypesByEquipmentLevel 
		select {_x select 0 == _equipmentLevel}
		select 0
		select 1;

	assert (count _applicableStereotypes > 0);
	
	private _stereotype = selectRandomWeighted _applicableStereotypes;

	private _uniform = selectRandomWeighted selectRandomWeighted (_stereotype select 0);
	private _vest = selectRandomWeighted selectRandomWeighted (_stereotype select 1);
	private _headgear = selectRandomWeighted selectRandomWeighted (_stereotype select 2);
	private _facewear = selectRandomWeighted selectRandomWeighted (_stereotype select 3);
	private _weaponAndAmmo = selectRandomWeighted selectRandomWeighted (_stereotype select 4);
	private _weapon = _weaponAndAmmo select 0;
	private _ammo = _weaponAndAmmo select 1;

	// Patch glitchy combinations
	if (_facewear == "G_Balaclava_blk" && _headgear in Foley_casualHeadgear) then {
		_headgear = "";
	};

	if (_vest != "" && _uniform in Foley_fatUniforms) then {
		_uniform = "UK3CB_NAP_B_U_Tshirt_BLK";
	};

	[_uniform, _vest, _headgear, _facewear, _weapon, _ammo]
};

#define EQUIPMENT_LEVEL_SPREAD 0.1

Foley_fnc_generateLoadouts = {
	params ["_count", "_averageEquipmentLevelPct"];

	private _levels = Foley_stereotypesByEquipmentLevel apply {_x select 0};
	private _averageEquipmentLevel = linearConversion [
		0, 100, _averageEquipmentLevelPct, (selectMin _levels), (selectMax _levels), true
	];
	private _levelsRange = (selectMax _levels) - (selectMin _levels);
	private _minimalDivisor = linearConversion [0.0, 1.0, EQUIPMENT_LEVEL_SPREAD, 0.25, _levelsRange];
	private _levelsWeights = _levels apply {
		1 / (_minimalDivisor max ((_x - _averageEquipmentLevel) ^ 2))
	};

	private _loadouts = [];
	for "_i" from 1 to _count do {
		private _equipmentLevel = _levels selectRandomWeighted _levelsWeights;
		_loadouts pushBack ([_equipmentLevel] call Foley_fnc_generateLoadout);
	};
	
	_loadouts
};
